apply plugin: 'kotlin-kapt'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

dependencies {
    implementation project(':kotlin-builder-annotation')

    implementation 'com.squareup:kotlinpoet:1.0.0-RC1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
}


task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            groupId 'com.thinkinglogic.builder'
            artifactId 'kotlin-builder-processor'
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'Kotlin Builder Annotation Processor'
                description = 'A Kapt processor for kotlin-builder-annotation - replacement for the Lombok @Builder annotation for Kotlin'
                url = 'https://github.com/ThinkingLogic/kotlin-builder-annotation'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'yetanothermatt'
                        name = 'Matthew Smith'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/ThinkingLogic/kotlin-builder-annotation.git'
                    developerConnection = 'scm:git:ssh:git@github.com:ThinkingLogic/kotlin-builder-annotation.git'
                    url = 'https://github.com/ThinkingLogic/kotlin-builder-annotation'
                }
            }
        }
    }
    repositories {
        maven {
            // change URLs to point to your repos, e.g. http://my.org/repo
            def releasesRepoUrl = "$buildDir/repos/releases"
            def snapshotsRepoUrl = "$buildDir/repos/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html4', true)
    }
}
/* see: https://github.com/bintray/gradle-bintray-plugin */
bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')
    publications = ['MyPublication']
    pkg {
        repo = 'maven'
        name = 'kotlin-builder-processor'
        userOrg = 'thinkinglogic'
        licenses = ['MIT']
        labels = ['kotlin', 'builder', 'annotation', 'processor']
        vcsUrl = 'https://github.com/ThinkingLogic/kotlin-builder-annotation.git'
        version {
            name = project.version
            desc = '@Builder annotation processor for Kotlin'
            released  = new Date()
            vcsTag = project.version
            attributes = ['kotlin-builder-processor': 'com.thinkinglogic:com.thinkinglogic.builder:kotlin-builder-processor']
            gpg {
                sign = true
            }
        }
    }
}
