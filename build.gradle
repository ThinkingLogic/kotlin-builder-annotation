buildscript {
    ext.kotlin_version = '1.3.50'
    ext.kotlintest_version = '3.3.2'
    ext.junit_jupiter_version = '5.3.1'
    ext.nexus_plugin_version = '1.1.0'

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'signing'
    id "org.jetbrains.dokka" version "1.4.20"
    id 'maven-publish'
    id 'io.github.gradle-nexus.publish-plugin' version "$nexus_plugin_version"
}
/**
 * to publish:
 *  ./gradlew clean publish
 */
/* see: https://github.com/gradle-nexus/publish-plugin,  https://h4pehl.medium.com/publish-your-gradle-artifacts-to-maven-central-f74a0af085b1 */
nexusPublishing {
    repositories {
        sonatype {
            username = System.getenv('SONATYPE_USER')
            password = System.getenv('SONATYPE_PASSWORD')
        }
    }
}


allprojects {
    group = 'com.thinkinglogic.builder'
    version = '1.2.1'
    apply plugin: "kotlin"

    repositories {
        mavenCentral()
        jcenter()
    }

    compileKotlin {
        kotlinOptions {
            jvmTarget = "11"
            javaParameters = true
        }
    }

    compileTestKotlin {
        kotlinOptions {
            jvmTarget = "11"
            javaParameters = true
        }
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
        testImplementation "org.junit.jupiter:junit-jupiter-engine:$junit_jupiter_version"
        testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_jupiter_version"
        testImplementation "io.kotlintest:kotlintest-runner-junit5:$kotlintest_version"
        testImplementation "io.kotlintest:kotlintest-core:$kotlintest_version"
    }

    test {
        useJUnitPlatform()
    }
}
