buildscript {
    ext.nexus_plugin_version = "1.1.0"

    repositories {
        mavenCentral()
    }
}

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.8.0"
    id "signing"
    id "org.jetbrains.dokka" version "1.4.20"
    id "maven-publish"
    id "io.github.gradle-nexus.publish-plugin" version "$nexus_plugin_version"
}
/**
 * to publish: (just run `./gradlew clean publish closeAndReleaseStagingRepository` if you"re feeling confident)
 * 1 `./gradlew clean publish`
 * 2 visit https://oss.sonatype.org/#nexus-search;quick~com.thinkinglogic.builder to confirm it looks correct
 * 3 visit https://oss.sonatype.org/#stagingRepositories and close then release the staging repository
 *
 * Note that this requires your pgp key to be publically identifiable, e.g. at https://keys.openpgp.org/
 * see: https://github.com/gradle-nexus/publish-plugin,  https://h4pehl.medium.com/publish-your-gradle-artifacts-to-maven-central-f74a0af085b1
 **/
nexusPublishing {
    repositories {
        sonatype {
            username = System.getenv("SONATYPE_USER")
            password = System.getenv("SONATYPE_PASSWORD")
        }
    }
}


allprojects {
    group = "com.thinkinglogic.builder"
    version = "1.2.3"
    apply plugin: "kotlin"

    repositories {
        mavenCentral()
    }

    kotlin {
        jvmToolchain {
            languageVersion.set(JavaLanguageVersion.of("11"))
        }
    }

    dependencies {
        testImplementation "org.jetbrains.kotlin:kotlin-test"
    }

    test {
        useJUnitPlatform()
    }
}
